"""
Explanation of wildcards:

dataType: input data types, e.g., no CNVs, including CNVs, excluding CNVs, etc.
datasetName: simulated dataset names, e.g., 0001, 0002, 0003...
"""


include: "scripts/constants.py"
include: "scripts/utils.py"


#########################################################
#              Get categories of wildcards              #
#########################################################

SIMUDATASETNAMES = get_dataset_names(SIMUSNVSITESDIR)
SIMUDATATYPES = get_data_types(
    config["benchmark"]["simulatedDataTypes"],
    config["benchmark"]["simulation"]["dataTypes"]["CNVTypes"]
)


########################################################
#                 SIFIT - From Monovar                 #
########################################################

# Rule: run SIFIT with TRUE parameters and filtered SNVs called by Monovar working with TRUE parameters
rule runSifitWithTrueMonovarSNVsTrueParams:
    input:
        cellNames=MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.sifit_cell_names",
        data=MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.sifit_data"
    output:
        cellNames=protected(SIFITTRUEMONOVARSNVSTRUEPARAMSLOCALDIR + "{dataType}/{datasetName}/filtered.sifit_cell_names"),
        data=protected(SIFITTRUEMONOVARSNVSTRUEPARAMSLOCALDIR + "{dataType}/{datasetName}/filtered.sifit_data"),
        out=protected(SIFITTRUEMONOVARSNVSTRUEPARAMSLOCALDIR + "{dataType}/{datasetName}/true_out"),
        tree=protected(SIFITTRUEMONOVARSNVSTRUEPARAMSLOCALDIR + "{dataType}/{datasetName}/filtered.sifit_data_mlTree.newick")
    params:
        sifit=get_sifit_jar_abs_path(config["tools"]["sifitLocal"]),
        loops=config["fineTune"]["sifit"]["loops"],
        fp=config["benchmark"]["analysis"]["effSeqErrRate"],
        fn=config["benchmark"]["analysis"]["adoRate"],
        cellNum=lambda wildcards, input: get_item_num(input.cellNames),
        siteNum=lambda wildcards, input: get_line_num(input.data)
    priority:
        100
    benchmark:
        repeat(EFFICIENCYSIFITDIR + "{dataType}/{datasetName}.tsv", config["benchmark"]["efficiency"]["repetition"])
    shell:
        "cp -f {input.cellNames} {output.cellNames}; "
        "cp -f {input.data} {output.data}; "
        "java -jar {params.sifit} "
        "-m {params.cellNum} "
        "-n {params.siteNum} "
        "-fp {params.fp} "
        "-fn {params.fn} "
        "-iter {params.loops} "
        "-df 1 -ipMat {output.data} "
        "-cellNames {output.cellNames} "
        "&>{output.out}"


#######################################################
#               Gather trees from SIFIT               #
#######################################################

# Rule: gather inferred trees from SIFIT with TRUE parameters and filtered SNVs called by Monovar working with TRUE parameters
rule gatherSifitTreesTrueMonovarSNVsTrueParams:
    input:
        inferredTrees = expand(SIFITTRUEMONOVARSNVSTRUEPARAMSLOCALDIR + "{dataType}/{datasetName}/filtered.sifit_data_mlTree.newick", dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        trueTrees = expand(SIMUCONVERTEDTRUETREEDIR + config["benchmark"]["simulation"]["treesWithoutTrunkWithoutNormalDir"] + config["benchmark"]["simulation"]["treesPre"] + ".{datasetName}", datasetName=SIMUDATASETNAMES)
    output:
        protected(ANALYSISTREECOMPDIR + "from_sifit_" + config["benchmark"]["analysis"]["trueMonovarSNVs"] + "_" + config["benchmark"]["analysis"]["trueParameters"] + ".tsv")
    params:
        datasetNames=SIMUDATASETNAMES,
        trueTreesDir=SIMUCONVERTEDTRUETREEDIR + config["benchmark"]["simulation"]["treesWithoutTrunkWithoutNormalDir"],
        trueTreesPre=config["benchmark"]["simulation"]["treesPre"],
        tool="sifit",
        snvType=config["benchmark"]["analysis"]["trueMonovarSNVs"],
        toolSetup=config["benchmark"]["analysis"]["trueParameters"],
        fineTuneType=None,
        dataType=SIMUDATATYPES,
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"]
    script:
        "scripts/gather_inferred_trees_part.py"


########################################################
#               Get estimates from SIFIT               #
########################################################

# Rule: get parameter estimates from SIFIT with TRUE parameters and filtered SNVs called by Monovar working with TRUE parameters
rule getSifitParamsWithTrueMonovarSNVsTrueParams:
    input: 
        expand(SIFITTRUEMONOVARSNVSTRUEPARAMSLOCALDIR + "{dataType}/{datasetName}/true_out", dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES)
    output: 
        protected(ANALYSISPARAMSDIR + "from_sifit_" + config["benchmark"]["analysis"]["trueMonovarSNVs"] + "_" + config["benchmark"]["analysis"]["trueParameters"] + ".tsv")
    params:
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        datasetNames=SIMUDATASETNAMES,
        tool="sifit",
        snvType=config["benchmark"]["analysis"]["trueMonovarSNVs"],
        toolSetup=config["benchmark"]["analysis"]["trueParameters"],
        fineTuneType=None,
        dataType=SIMUDATATYPES
    script:
        "scripts/get_param_estimates_sifit.py"


#######################################################
#        Gather the number of sites from SIFIT        #
#######################################################

# Rule: gather the number of variant sites as input to SIFIT
rule gatherSifitSitesInfoWithTrueMonovarSNVsTrueParams:
    input:
        data=expand(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.sifit_data", dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES)
    output:
        protected(ANALYSISSITESINFODIR + "from_sifit_" + config["benchmark"]["analysis"]["trueMonovarSNVs"] + "_" + config["benchmark"]["analysis"]["trueParameters"] + ".tsv")
    params:
        datasetNames=SIMUDATASETNAMES,
        tool="sifit",
        snvType=config["benchmark"]["analysis"]["trueMonovarSNVs"],
        toolSetup=config["benchmark"]["analysis"]["trueParameters"],
        fineTuneType=None,
        dataType=SIMUDATATYPES,
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"]
    script:
        "scripts/gather_sites_info_sifit.py"

