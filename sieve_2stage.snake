"""
Explanation of wildcards:

fineTuneTypeSieve: types of fine-tuning to perform for SIEVE defined in the configuration file, e.g., single thread, multi thread, etc.
dataType: input data types, e.g., no CNVs, including CNVs, excluding CNVs, etc.
datasetName: simulated dataset names, e.g., 0001, 0002, 0003...
runTemplate: configuration templates for SIEVE.
runTemplateStage1: configuration templates for SIEVE which will be the
first stage of the two-stage strategy.
runTemplateStage2: configuration templates for SIEVE which will be the
second stage of the two-stage strategy.
"""


include: "scripts/constants.py"
include: "scripts/utils.py"


#########################################################
#              Get categories of wildcards              #
#########################################################

SIMUDATASETNAMES = get_dataset_names(SIMUSNVSITESDIR)
FINETUNETYPESSIEVE = get_fine_tune_types(
    config["fineTune"]["beast"],
    config["benchmark"]["fineTuneTypes"]
)
SIMUDATATYPES = get_data_types(
    config["benchmark"]["simulatedDataTypes"],
    config["benchmark"]["simulation"]["dataTypes"]["CNVTypes"]
)
TEMPLATES = config["configFiles"]["sieve"]["fileNames"]
STAGE1TEMPLATES = config["configFiles"]["sieve"]["stage1FileNames"]
STAGE2TEMPLATES = config["configFiles"]["sieve"]["stage2FileNames"]

wildcard_constraints:
    fineTuneTypeSieve='|'.join([re.escape(i) for i in FINETUNETYPESSIEVE]) if FINETUNETYPESSIEVE is not None and len(FINETUNETYPESSIEVE) > 0 else NONE_PATTERN,
    dataType='|'.join([re.escape(i) for i in SIMUDATATYPES]) if SIMUDATATYPES is not None and len(SIMUDATATYPES) > 0 else NONE_PATTERN,
    datasetName='|'.join([re.escape(i) for i in SIMUDATASETNAMES]) if SIMUDATASETNAMES is not None and len(SIMUDATASETNAMES) > 0 else NONE_PATTERN,
    runTemplate='|'.join([re.escape(i) for i in TEMPLATES]) if TEMPLATES is not None and len(TEMPLATES) > 0 else NONE_PATTERN,
    runTemplateStage1='|'.join([re.escape(i) for i in STAGE1TEMPLATES]) if STAGE1TEMPLATES is not None and len(STAGE1TEMPLATES) > 0 else NONE_PATTERN,
    runTemplateStage2='|'.join([re.escape(i) for i in STAGE2TEMPLATES]) if STAGE2TEMPLATES is not None and len(STAGE2TEMPLATES) > 0 else NONE_PATTERN
        

#######################################################
#                                                     #
#               SIEVE - From candidates               #
#                                                     #
#######################################################


#######################################################
#          SIEVE - From candidates - Stage 1          #
#######################################################

# Rule: select candidate SNVs with datafilter
rule selectCandidateSNVsForSieve:
    input:
        flagFile=SIMUDATATYPESDIR + "DONE_FLAG",
        mpileupFile=SIMUDATATYPESDIR + "{dataType}/{datasetName}/read_counts" + config["benchmark"]["simulation"]["MPileUpDataSuf"],
        bamNames=SIMUSCIPHIDIR + config["benchmark"]["simulation"]["bamFileNamesWithoutNormal"]
    output:
        protected(SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/readCounts.tsv")
    params:
        dataFilter=config["tools"]["dataFilter"]
    shell:
        "{params.dataFilter} "
        "--cellNames {input.bamNames} "
        "--in {input.mpileupFile} "
        "-o {output}"

# Rule: integrate reads data from candidates into templates for SIEVE
rule integrateCandidateSNVsForSieve:
    input:
        SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/readCounts.tsv"
    output:
        protected(SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve" + config["configFiles"]["sieve"]["suffix"])
    params:
        dataCollector=config["tools"]["applauncher"] + " DataCollectorLauncher",
        templates=config["configFiles"]["sieve"]["dir"] + "{runTemplate}" + config["configFiles"]["sieve"]["suffix"]
    shell:
        "{params.dataCollector} "
        "-cell {SIMUTUMORCELLNAMES} "
        "-data {input} "
        "-template {params.templates} "
        "-out {output}"

# Rule: perfrom phylogenetic inference with SIEVE using Sciphi candicate SNVs
rule runSieveWithCandidateSNVsPhyloInf:
    input:
        rawData=SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/readCounts.tsv",
        configuredData=SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve" + config["configFiles"]["sieve"]["suffix"]
    output:
        trees=protected(SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve.trees"),
        mcmclog=protected(SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve.log"),
        state=protected(SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve" + config["configFiles"]["sieve"]["suffix"] + ".state")
    params:
        beast=config["tools"]["beast"],
        seed=lambda wildcards: get_dict_values(
            config,
            ["fineTune", "beast", wildcards.fineTuneTypeSieve, "seed"]
        ),
        prefix=SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/",
        log=SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/out"
    threads:
        lambda wildcards, input: get_thread_num_for_sieve(
            input.rawData,
            get_dict_values(
                config,
                ["fineTune", "beast", wildcards.fineTuneTypeSieve, "nrOfSitesPerThreadStage1"]
            )
        )
    priority:
        100
    benchmark:
        repeat(EFFICIENCYSIEVEDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}.tsv", config["benchmark"]["efficiency"]["repetition"])
    shell:
        "{params.beast} "
        "-overwrite "
        "-seed {params.seed} "
        "-threads {threads} "
        "-prefix {params.prefix} "
        "{input.configuredData} " 
        "&>{params.log}"

# Rule: perfrom tree annotation with SIEVE using Sciphi candicate SNVs
rule runSieveWithCandidateSNVsTreeAnnotation:
    input:
        SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve.trees"
    output:
        tree=protected(SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve.tree"),
        simpleTree=protected(SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve_simple.tree")
    params:
        treeAnnotator=config["tools"]["applauncher"] + " ScsTreeAnnotatorLauncher",
        burnIn=config["fineTune"]["treeAnnotator"]["burnIn"],
        log=SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/process_tree.out"
    shell:
        "{params.treeAnnotator} "
        "-burnin {params.burnIn} "
        "-simpleTree "
        "{input} "
        "{output.tree} "
        "&>{params.log}"

# Rule: perfrom variant calling with SIEVE using Sciphi candicate SNVs
rule runSieveWithCandidateSNVsVarCall:
    input:
        mcmclog=SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve.log",
        config=SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve" + config["configFiles"]["sieve"]["suffix"],
        tree=SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve.tree"
    output:
        estimates=protected(SIEVEFROMCANDIDATEVARCALDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve.estimates"),
        files=protected(SIEVEFROMCANDIDATEVARCALDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/files")
    params:
        variantCaller=config["tools"]["applauncher"] + " VariantCallerLauncher",
        burnIn=config["fineTune"]["variantCaller"]["burnIn"],
        estimates=config["fineTune"]["variantCaller"]["estimates"],
        prefix=SIEVEFROMCANDIDATEVARCALDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/"
    threads:
        config["fineTune"]["variantCaller"]["threads"]
    shell:
        "{params.variantCaller} "
        "-details "
        "-burnin {params.burnIn} "
        "-threads {threads} "
        "-estimates {params.estimates} "
        "-mcmclog {input.mcmclog} "
        "-config {input.config} "
        "-tree {input.tree} "
        "-prefix {params.prefix} "
        "&>{params.prefix}out; "
        "ls {params.prefix} > {output.files}"


#######################################################
#          SIEVE - From candidates - Stage 2          #
#######################################################

# Rule: update templates for stage 2 from results of stage 1
rule updateStage2ConfigCandidateSNVsForSieve:
    input:
        estimates=SIEVEFROMCANDIDATEVARCALDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}/{datasetName}/sieve.estimates",
        files=SIEVEFROMCANDIDATEVARCALDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}/{datasetName}/files",
        simpleTree=SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}/{datasetName}/sieve_simple.tree"
    output:
        protected(SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/stage2_template" + config["configFiles"]["sieve"]["suffix"])
    params:
        template1=config["configFiles"]["sieve"]["dir"] + "{runTemplateStage1}" + config["configFiles"]["sieve"]["suffix"],
        template2=config["configFiles"]["sieve"]["dir"] + "{runTemplateStage2}" + config["configFiles"]["sieve"]["suffix"]
    shell:
        "python scripts/set_up_stage2_from_stage1.py "
        "--tree {input.simpleTree} "
        "--estimates {input.estimates} "
        "--results {input.files} "
        "--template1 {params.template1} "
        "--template2 {params.template2} "
        "--out {output}"

# Rule: integrate reads data from Sciphi candicate SNVs into templates for stage 2 for SIEVE
rule integrateStage2CandidateSNVsForSieve:
    input:
        data=SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}/{datasetName}/readCounts.tsv",
        template=SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/stage2_template" + config["configFiles"]["sieve"]["suffix"]
    output:
        protected(SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve" + config["configFiles"]["sieve"]["suffix"])
    params:
        dataCollector=config["tools"]["applauncher"] + " DataCollectorLauncher",
    shell:
        "{params.dataCollector} "
        "-cell {SIMUTUMORCELLNAMES} "
        "-data {input.data} "
        "-template {input.template} "
        "-out {output}"

# Rule: perfrom phylogenetic inference for stage 2 with SIEVE using Sciphi candicate SNVs
rule runSieveWithCandidateSNVsPhyloInfStage2:
    input:
        rawData=SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}/{datasetName}/readCounts.tsv",
        configuredData=SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve" + config["configFiles"]["sieve"]["suffix"]
    output:
        trees=protected(SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve.trees"),
        mcmclog=protected(SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve.log"),
        state=protected(SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve" + config["configFiles"]["sieve"]["suffix"] + ".state")
    params:
        beast=config["tools"]["beast"],
        seed=lambda wildcards: get_dict_values(
            config,
            ["fineTune", "beast", wildcards.fineTuneTypeSieve, "seed"]
        ),
        prefix=SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/",
        log=SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/out"
    threads:
        lambda wildcards, input: get_thread_num_for_sieve(
            input.rawData,
            get_dict_values(
                config,
                ["fineTune", "beast", wildcards.fineTuneTypeSieve, "nrOfSitesPerThreadStage2"]
            )
        )
    benchmark:
        repeat(EFFICIENCYSIEVEDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}.tsv", config["benchmark"]["efficiency"]["repetition"])
    shell:
        "{params.beast} "
        "-overwrite "
        "-seed {params.seed} "
        "-threads {threads} "
        "-prefix {params.prefix} "
        "{input.configuredData} " 
        "&>{params.log}"

# Rule: perfrom tree annotation for stage 2 with SIEVE using Sciphi candicate SNVs
rule runSieveWithCandidateSNVsTreeAnnotationStage2:
    input:
        SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve.trees"
    output:
        tree=protected(SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve.tree"),
        simpleTree=protected(SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve_simple.tree")
    params:
        treeAnnotator=config["tools"]["applauncher"] + " ScsTreeAnnotatorLauncher",
        burnIn=config["fineTune"]["treeAnnotator"]["burnIn"],
        log=SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/process_tree.out"
    shell:
        "{params.treeAnnotator} "
        "-burnin {params.burnIn} "
        "-simpleTree "
        "{input} "
        "{output.tree} "
        "&>{params.log}"

# Rule: perfrom variant calling for stage 2 with SIEVE using Sciphi candicate SNVs
rule runSieveWithCandidateSNVsVarCallStage2:
    input:
        mcmclog=SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve.log",
        config=SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve" + config["configFiles"]["sieve"]["suffix"],
        tree=SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve.tree"
    output:
        estimates=protected(SIEVEFROMCANDIDATEVARCALSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve.estimates"),
        files=protected(SIEVEFROMCANDIDATEVARCALSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/stage2_files")
    params:
        variantCaller=config["tools"]["applauncher"] + " VariantCallerLauncher",
        burnIn=config["fineTune"]["variantCaller"]["burnIn"],
        prefix=SIEVEFROMCANDIDATEVARCALSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/"
    threads:
        config["fineTune"]["variantCaller"]["threads"]
    shell:
        "{params.variantCaller} "
        "-details "
        "-burnin {params.burnIn} "
        "-threads {threads} "
        "-estimates median "
        "-mcmclog {input.mcmclog} "
        "-config {input.config} "
        "-tree {input.tree} "
        "-prefix {params.prefix} "
        "&>{params.prefix}out; "
        "ls {params.prefix} > {output.files}"


#######################################################
#               Gather trees from SIEVE               #
#######################################################

# Rule: gather inferred trees from SIEVE with candidate SNVs
rule gatherSieveTreesWithCandidateSNVs:
    input:
        inferredTrees=expand(SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve_simple.tree", fineTuneTypeSieve=FINETUNETYPESSIEVE, dataType=SIMUDATATYPES, runTemplate=TEMPLATES, datasetName=SIMUDATASETNAMES),
        trueTrees=expand(SIMUCONVERTEDTRUETREEDIR + config["benchmark"]["simulation"]["treesWithTrunkWithoutNormalDir"] + config["benchmark"]["simulation"]["treesPre"] + ".{datasetName}", datasetName=SIMUDATASETNAMES)
    output:
        protected(ANALYSISTREECOMPDIR + "from_sieve_" + config["benchmark"]["analysis"]["candidateSNVs"] + ".tsv")
    params:
        datasetNames=SIMUDATASETNAMES,
        trueTreesDir=SIMUCONVERTEDTRUETREEDIR + config["benchmark"]["simulation"]["treesWithTrunkWithoutNormalDir"],
        trueTreesPre=config["benchmark"]["simulation"]["treesPre"],
        tool="sieve",
        snvType=config["benchmark"]["analysis"]["candidateSNVs"],
        toolSetup=TEMPLATES,
        fineTuneType=FINETUNETYPESSIEVE,
        dataType=SIMUDATATYPES,
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"]
    script:
        "scripts/gather_inferred_trees_part.py"

# Rule: gather inferred trees from SIEVE stage 2 with candidate SNVs
rule gatherSieveStage2TreesWithCandidateSNVs:
    input:
        inferredTrees=expand(SIEVEFROMCANDIDATEPHYINFSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve_simple.tree", fineTuneTypeSieve=FINETUNETYPESSIEVE, dataType=SIMUDATATYPES, runTemplateStage1=STAGE1TEMPLATES, runTemplateStage2=STAGE2TEMPLATES, datasetName=SIMUDATASETNAMES),
        trueTrees=expand(SIMUCONVERTEDTRUETREEDIR + config["benchmark"]["simulation"]["treesWithTrunkWithoutNormalDir"] + config["benchmark"]["simulation"]["treesPre"] + ".{datasetName}", datasetName=SIMUDATASETNAMES)
    output:
        protected(ANALYSISTREECOMPDIR + "from_sieve_stage2_" + config["benchmark"]["analysis"]["candidateSNVs"] + ".tsv")
    params:
        datasetNames=SIMUDATASETNAMES,
        trueTreesDir=SIMUCONVERTEDTRUETREEDIR + config["benchmark"]["simulation"]["treesWithTrunkWithoutNormalDir"],
        trueTreesPre=config["benchmark"]["simulation"]["treesPre"],
        tool="sieve",
        snvType=config["benchmark"]["analysis"]["candidateSNVs"],
        toolSetup=expand("{runTemplateStage1}-{runTemplateStage2}", runTemplateStage1=STAGE1TEMPLATES, runTemplateStage2=STAGE2TEMPLATES),
        fineTuneType=FINETUNETYPESSIEVE,
        dataType=SIMUDATATYPES,
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"]
    script:
        "scripts/gather_inferred_trees_part.py"


########################################################
#               Get estimates from SIEVE               #
########################################################

# Rule: get parameter estimates from SIEVE with candidate SNVs
rule getSieveParamsWithCandidateSNVs:
    input:
        expand(SIEVEFROMCANDIDATEVARCALDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/sieve.estimates", fineTuneTypeSieve=FINETUNETYPESSIEVE, dataType=SIMUDATATYPES, runTemplate=TEMPLATES, datasetName=SIMUDATASETNAMES)
    output:
        protected(ANALYSISPARAMSDIR + "from_sieve_" + config["benchmark"]["analysis"]["candidateSNVs"] + ".tsv")
    params:
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        toolSetup=TEMPLATES,
        datasetNames=SIMUDATASETNAMES,
        tool="sieve",
        snvType=config["benchmark"]["analysis"]["candidateSNVs"],
        fineTuneType=FINETUNETYPESSIEVE,
        dataType=SIMUDATATYPES,
        estimatesType=config["fineTune"]["variantCaller"]["estimates"]
    script:
        "scripts/get_param_estimates_sieve.py"

# Rule: get parameter estimates from SIEVE stage 2 with candidate SNVs
rule getSieveStage2ParamsWithCandidateSNVs:
    input: 
        expand(SIEVEFROMCANDIDATEVARCALSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/sieve.estimates", fineTuneTypeSieve=FINETUNETYPESSIEVE, dataType=SIMUDATATYPES, runTemplateStage1=STAGE1TEMPLATES, runTemplateStage2=STAGE2TEMPLATES, datasetName=SIMUDATASETNAMES)
    output: 
        protected(ANALYSISPARAMSDIR + "from_sieve_stage2_" + config["benchmark"]["analysis"]["candidateSNVs"] + ".tsv")
    params:
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        toolSetup=expand("{runTemplateStage1}-{runTemplateStage2}", runTemplateStage1=STAGE1TEMPLATES, runTemplateStage2=STAGE2TEMPLATES),
        datasetNames=SIMUDATASETNAMES,
        tool="sieve",
        snvType=config["benchmark"]["analysis"]["candidateSNVs"],
        fineTuneType=FINETUNETYPESSIEVE,
        dataType=SIMUDATATYPES,
        estimatesType=config["fineTune"]["variantCaller"]["estimates"]
    script:
        "scripts/get_param_estimates_sieve.py"


########################################################
#        Get variant calling results from SIEVE        #
########################################################

# Rule: gather variant calling results from SIEVE with candidate SNVs
rule gatherSieveVarResultsCandidateSNVs:
    input: 
        allFiles=expand(SIEVEFROMCANDIDATEVARCALDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/files", fineTuneTypeSieve=FINETUNETYPESSIEVE, dataType=SIMUDATATYPES, runTemplate=TEMPLATES, datasetName=SIMUDATASETNAMES),
        trueCellNames=SIMUTUMORCELLNAMES,
        trueSNVSites=expand(SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["trueSNVSitesNamesPre"], dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        trueSNVGenotypesNU=expand(SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["trueSNVGenotypesNUPre"], dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        trueSNVGenotypesTer=expand(SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["trueSNVGenotypesTerPre"] + '.simple', dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES)
    output: 
        protected(ANALYSISVARCALLDIR + "from_sieve_" + config["benchmark"]["analysis"]["candidateSNVs"] + ".tsv")
    params:
        forSieve=True,
        mergedDuplicateLines=False,
        datasetNames=SIMUDATASETNAMES,
        tool="sieve",
        snvType=config["benchmark"]["analysis"]["candidateSNVs"],
        toolSetup=TEMPLATES,
        fineTuneType=FINETUNETYPESSIEVE,
        dataType=SIMUDATATYPES,
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"]
    script:
        "scripts/gather_variant_calling_results_part.py"

# Rule: gather variant calling results from SIEVE stage 2 with candidate SNVs
rule gatherSieveStage2VarResultsCandidateSNVs:
    input: 
        allFiles=expand(SIEVEFROMCANDIDATEVARCALSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/stage2_files", fineTuneTypeSieve=FINETUNETYPESSIEVE, dataType=SIMUDATATYPES, runTemplateStage1=STAGE1TEMPLATES, runTemplateStage2=STAGE2TEMPLATES, datasetName=SIMUDATASETNAMES),
        trueCellNames=SIMUTUMORCELLNAMES,
        trueSNVSites=expand(SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["trueSNVSitesNamesPre"], dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        trueSNVGenotypesNU=expand(SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["trueSNVGenotypesNUPre"], dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        trueSNVGenotypesTer=expand(SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["trueSNVGenotypesTerPre"] + '.simple', dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES)
    output: 
        protected(ANALYSISVARCALLDIR + "from_sieve_stage2_" + config["benchmark"]["analysis"]["candidateSNVs"] + ".tsv")
    params:
        forSieve=True,
        mergedDuplicateLines=False,
        datasetNames=SIMUDATASETNAMES,
        tool="sieve",
        snvType=config["benchmark"]["analysis"]["candidateSNVs"],
        toolSetup=expand("{runTemplateStage1}-{runTemplateStage2}", runTemplateStage1=STAGE1TEMPLATES, runTemplateStage2=STAGE2TEMPLATES),
        fineTuneType=FINETUNETYPESSIEVE,
        dataType=SIMUDATATYPES,
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"]
    script:
        "scripts/gather_variant_calling_results_part.py"


########################################################
#          Get ADO calling results from SIEVE          #
########################################################

# Rule: gather ADO calling results from SIEVE with candidate SNVs
rule gatherSieveAdoResultsCandidateSNVs:
    input:
        allFiles=expand(SIEVEFROMCANDIDATEVARCALDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplate}/{datasetName}/files", fineTuneTypeSieve=FINETUNETYPESSIEVE, dataType=SIMUDATATYPES, runTemplate=TEMPLATES, datasetName=SIMUDATASETNAMES),
        trueCellNames=SIMUTUMORCELLNAMES
    output: 
        protected(ANALYSISADOCALLDIR + "from_sieve_" + config["benchmark"]["analysis"]["candidateSNVs"] + ".tsv")
    params:
        datasetNames=SIMUDATASETNAMES,
        tool="sieve",
        snvType=config["benchmark"]["analysis"]["candidateSNVs"],
        toolSetup=TEMPLATES,
        fineTuneType=FINETUNETYPESSIEVE,
        dataType=SIMUDATATYPES,
        trueAdoStatesPre=SIMUTRUEADOSTATESDIR + config["benchmark"]["simulation"]["trueAdoStatesPre"],
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"]
    script:
        "scripts/gather_ado_calling_results_part.py"

# Rule: gather ADO calling results from SIEVE stage 2 with candidate SNVs
rule gatherSieveStage2AdoResultsCandidateSNVs:
    input: 
        allFiles=expand(SIEVEFROMCANDIDATEVARCALSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/stage2_files", fineTuneTypeSieve=FINETUNETYPESSIEVE, dataType=SIMUDATATYPES, runTemplateStage1=STAGE1TEMPLATES, runTemplateStage2=STAGE2TEMPLATES, datasetName=SIMUDATASETNAMES),
        trueCellNames=SIMUTUMORCELLNAMES
    output: 
        protected(ANALYSISADOCALLDIR + "from_sieve_stage2_" + config["benchmark"]["analysis"]["candidateSNVs"] + ".tsv")
    params:
        datasetNames=SIMUDATASETNAMES,
        tool="sieve",
        snvType=config["benchmark"]["analysis"]["candidateSNVs"],
        toolSetup=expand("{runTemplateStage1}-{runTemplateStage2}", runTemplateStage1=STAGE1TEMPLATES, runTemplateStage2=STAGE2TEMPLATES),
        fineTuneType=FINETUNETYPESSIEVE,
        dataType=SIMUDATATYPES,
        trueAdoStatesPre=SIMUTRUEADOSTATESDIR + config["benchmark"]["simulation"]["trueAdoStatesPre"],
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"]
    script:
        "scripts/gather_ado_calling_results_part.py"


#######################################################
#        Gather the number of sites from SIEVE        #
#######################################################

# Rule: gather the number of candidate variant sites as input to SIEVE
rule gatherSieveSitesInfo:
    input:
        expand(SIEVEFROMCANDIDATEPHYINFDIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}/{datasetName}/readCounts.tsv", fineTuneTypeSieve=FINETUNETYPESSIEVE, dataType=SIMUDATATYPES, runTemplateStage1=STAGE1TEMPLATES, datasetName=SIMUDATASETNAMES)
    output:
        protected(ANALYSISSITESINFODIR + "from_sieve_" + config["benchmark"]["analysis"]["candidateSNVs"] + ".tsv")
    params:
        datasetNames=SIMUDATASETNAMES,
        tool="sieve",
        snvType=config["benchmark"]["analysis"]["candidateSNVs"],
        toolSetup=STAGE1TEMPLATES,
        fineTuneType=FINETUNETYPESSIEVE,
        dataType=SIMUDATATYPES,
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"]
    script:
        "scripts/gather_sites_info_sieve.py"


########################################################
#                                                      #
#          Get allelic information from SIEVE          #
#                                                      #
########################################################

# Rule: generate allelic information per cell from true allelic sequencing information and true size factorand SIEVE results
rule generateAllelicInfoPerCell:
    input:
        cellNames=SIMUTUMORCELLNAMES,
        trueSizeFactors=SIMUTRUESIZEFACTORSDIR + config["benchmark"]["simulation"]["trueSizeFactorsPre"] + ".{datasetName}",
        trueAllelicSeqInfo=SIMUALLELICSEQINFODIR + config["benchmark"]["simulation"]["trueAllelicSeqInfoSNVsPre"] + ".{datasetName}",
        sieveVarCallFiles=SIEVEFROMCANDIDATEVARCALSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/stage2_files"
    output:
        protected(SIEVEFROMCANDIDATEVARCALSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/allelic_info.rds")
    params:
        tool="sieve",
        toolSetup=lambda wildcards: "{runTemplateStage1}-{runTemplateStage2}".format(runTemplateStage1=wildcards.runTemplateStage1, runTemplateStage2=wildcards.runTemplateStage2),
        datasetLabel=lambda wildcards: "{datasetName}".format(datasetName=wildcards.datasetName),
        fineTuneType=lambda wildcards: "{fineTuneTypeSieve}".format(fineTuneTypeSieve=wildcards.fineTuneTypeSieve),
        dataType=lambda wildcards: "{dataType}".format(dataType=wildcards.dataType),
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"]
    script:
        "scripts/generate_allelic_info_per_cell.R"

# Rule: combine allelic information per cell from all datasets
rule combineAllelicInfoPerCell:
    input:
        expand(SIEVEFROMCANDIDATEVARCALSTAGE2DIR + "{fineTuneTypeSieve}/{dataType}/{runTemplateStage1}-{runTemplateStage2}/{datasetName}/allelic_info.rds", fineTuneTypeSieve=FINETUNETYPESSIEVE, dataType=SIMUDATATYPES, runTemplateStage1=STAGE1TEMPLATES, runTemplateStage2=STAGE2TEMPLATES, datasetName=SIMUDATASETNAMES)
    output:
        protected(ANALYSISALLELICINFO + "allelic_info.rds")
    script:
        "scripts/combine_allelic_info.R"

